buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.3'
    }
}

// Required because retrolambda is on maven central
repositories {
    mavenCentral()
}

apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
    buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

    defaultConfig {
        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")
        versionCode globalConfiguration.getAt("androidVersionCode")

        buildConfigField 'String', 'TMDB_API_KEY', "\"${tmdbApiKey}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    def dataDependencies = rootProject.ext.dataDependencies

    compile project(':domain')
    compile dataDependencies.dagger
    compile dataDependencies.daggerCompiler
    compile dataDependencies.rxJava
    compile dataDependencies.rxAndroid
    compile dataDependencies.retrofit
    compile dataDependencies.gsonConverter
    compile dataDependencies.retrofitRxJava
    compile dataDependencies.storIo
    compile dataDependencies.storIoAnnotation
    apt dataDependencies.storIoProcessor
    compile dataDependencies.javaxAnnotation
}

