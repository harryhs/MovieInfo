// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'buildsystem/dependencies.gradle'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.7'
        classpath "com.android.databinding:dataBinder:1.0-rc2"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

def properties = loadProperties()

ext {
    tmdbApiKey = properties.getProperty("TMDB_API_KEY")
    keyPassword = properties.getProperty("KEY_PASSWORD")
    storePassword = properties.getProperty("STORE_PASSWORD")
    sdkPath = properties.getProperty('sdk.dir')
}

def loadProperties() {
    if (project.rootProject.file('local.properties').exists()) {
        return loadFromLocalProperties()
    } else {
        return loadFromEnvironment()
    }
}

def loadFromLocalProperties() {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
    return localProperties;
}

def loadFromEnvironment() {
    Properties localProperties = new Properties()
    localProperties.setProperty('TMDB_API_KEY', System.getenv("TMDB_API_KEY"))
    localProperties.setProperty('KEY_PASSWORD', System.getenv("KEY_PASSWORD"))
    localProperties.setProperty('STORE_PASSWORD', System.getenv("STORE_PASSWORD"))
    localProperties.setProperty('sdk.dir', System.getenv('sdk.dir'))
    return localProperties
}

allprojects {
    repositories {
        jcenter()
        maven { url "${sdkPath}" + '/extras/android/m2repository' }
    }
}