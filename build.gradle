// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: 'buildsystem/dependencies.gradle'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.7'
        classpath "com.android.databinding:dataBinder:1.0-rc2"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

def tmdbProperties = loadTmdbProperties()

ext {
    tmdbApiKey = tmdbProperties.getProperty("TMDB_API_KEY")
}

def loadTmdbProperties() {
    if (project.rootProject.file('local.properties').exists()) {
        return loadTmdbFromLocalProperties()
    } else {
        return loadTmdbFromEnvironment()
    }
}

def loadTmdbFromLocalProperties() {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())
    return localProperties;
}

def loadTmdbFromEnvironment() {
    Properties localProperties = new Properties()
    localProperties.setProperty('TMDB_API_KEY', System.getenv("TMDB_API_KEY"))
    return localProperties
}